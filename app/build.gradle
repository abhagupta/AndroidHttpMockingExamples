apply plugin: 'com.android.application'

android {
    compileSdkVersion 18
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "weatherview.joshskeen.com.weatherview"
        minSdkVersion 14
        targetSdkVersion 18
        buildConfigField "String", "WEATHERVIEW_API_KEY", "\"840dbdf2737a7ff9\"";
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Enabling Multidex support for 'androidTest' builds because of large dependencies
        multiDexEnabled = true
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    buildTypes {
        // def IP = InetAddress.localHost.hostAddress  // This is helpful if you want to point WireMock to your DEV machine and run an instance there
        def IP = "127.0.0.1"
        def IPS = "\"" + IP + "\""

        debug {
            buildConfigField "String", "IP", IPS
            buildConfigField "Integer", "PORT", "9998"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

def versions = [
        jackson: '2.6.1',
        jetty  : '9.2+'
]

dependencies {
    //------ Compile Dependencies ------
    compile('com.squareup.retrofit:retrofit:1.8.0')
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile "org.slf4j:slf4j-api:1.7.12"

    //------ Android Test Dependencies ------
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'junit', module: 'junit'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    androidTestCompile 'junit:junit:4.12'

    //MockWebServer - Version 2.2 of mockwebserver doesn't work because of an issue, so forcing v2.1 - https://github.com/square/okhttp/issues/1069
    androidTestCompile('com.squareup.okhttp:mockwebserver:2.1.0') {
        exclude module: 'okhttp'
    }

    //WireMock
    androidTestCompile("com.github.tomakehurst:wiremock:2.0.8-beta") {
        //Allows us to use the Android version of Apache httpclient
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'

        //Resolves the Duplicate Class Exception
        //Error:Execution failed for task ':app:transformClassesWithJarMergingForDebugAndroidTest'.
        //       > com.android.build.api.transform.TransformException: java.util.zip.ZipException: duplicate entry: org/objectweb/asm/AnnotationVisitor.class
        exclude group: 'asm', module: 'asm'
    }
    androidTestCompile 'org.apache.httpcomponents:httpclient-android:4.3.5+'
}
configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
    all*.exclude group: 'com.android.support', module: 'support-annotations'
}